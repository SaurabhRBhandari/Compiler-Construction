--
Group Number - 10
ID:	2021A7PS1463P			Name: Dhyey Italiya
ID:	2021A7PS2434P			Name: LAKSHIT SETHI
ID:	2021A7PS0523P			Name: Abir Abhyankar 
ID:	2021A7PS2414P			Name: Saksham Verma
ID:	2021A7PS2412P			Name: Saurabh Bhandari
--

Grammar

uprogram - otherFunctions mainFunction
mainFunction - TK_MAIN stmts TK_END
otherFunctions - function otherFunctions ; TK_EPSILON
function - TK_FUNID input_par output_par TK_SEM stmts TK_END
input_par - TK_INPUT TK_PARAMETER TK_LIST TK_SQL parameter_list TK_SQR
output_par - TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL parameter_list TK_SQR ; TK_EPSILON
parameter_list - dataType TK_ID remaining_list
dataType - primitiveDatatype ; constructedDatatype
primitiveDatatype - TK_INT ; TK_REAL
constructedDatatype - TK_RECORD TK_RUID ; TK_UNION TK_RUID ; TK_RUID
remaining_list - TK_COMMA parameter_list ; TK_EPSILON
stmts - typeDefinitions declarations otherStmts returnStmt
typeDefinitions - actualOrRedefined typeDefinitions ; TK_EPSILON
actualOrRedefined - typeDefinition ; definetypestmt
typeDefinition - TK_RECORD TK_RUID fieldDefinitions TK_ENDRECORD
typeDefinition - TK_UNION TK_RUID fieldDefinitions TK_ENDUNION
fieldDefinitions - fieldDefinition fieldDefinition moreFields
fieldDefinition - TK_TYPE fieldType  TK_COLON TK_FIELDID TK_SEM
fieldType - primitiveDatatype ; constructedDatatype 
moreFields - fieldDefinition moreFields ; TK_EPSILON
declarations - declaration declarations ; TK_EPSILON
declaration - TK_TYPE dataType TK_COLON TK_ID global_or_not TK_SEM
global_or_not - TK_COLON TK_GLOBAL ; TK_EPSILON
otherStmts - stmt otherStmts ; TK_EPSILON
stmt - assignmentStmt ; iterativeStmt ; conditionalStmt ; ioStmt ; funCallStmt
assignmentStmt - singleOrRecId TK_ASSIGNOP arithmeticExpression TK_SEM
singleOrRecId - TK_ID option_single_constructed
option_single_constructed - TK_EPSILON ; oneExpansion moreExpansions
oneExpansion - TK_DOT TK_FIELDID
moreExpansions - oneExpansion moreExpansions ; TK_EPSILON
funCallStmt - outputParameters TK_CALL TK_FUNID TK_WITH TK_PARAMETERS inputParameters TK_SEM
outputParameters - TK_SQL idList TK_SQR TK_ASSIGNOP ; TK_EPSILON
inputParameters - TK_SQL idList TK_SQR
iterativeStmt - TK_WHILE TK_OP booleanExpression TK_CL stmt otherStmts TK_ENDWHILE
conditionalStmt - TK_IF TK_OP booleanExpression TK_CL TK_THEN stmt otherStmts elsePart
elsePart - TK_ELSE stmt otherStmts TK_ENDIF ; TK_ENDIF
ioStmt - TK_READ TK_OP var TK_CL TK_SEM ; TK_WRITE TK_OP var TK_CL TK_SEM
arithmeticExpression - term expPrime
expPrime - lowPrecedenceOperators term expPrime ; TK_EPSILON
term - factor termPrime
termPrime - highPrecedenceOperator factor termPrime ; TK_EPSILON
factor - TK_OP arithmeticExpression TK_CL ; var
highPrecedenceOperator - TK_MUL ; TK_DIV
lowPrecedenceOperators - TK_PLUS ; TK_MINUS
booleanExpression - TK_OP booleanExpression TK_CL logicalOp TK_OP booleanExpression TK_CL
booleanExpression - var relationalOp var
booleanExpression - TK_NOT TK_OP booleanExpression TK_CL
var - singleOrRecId ; TK_NUM ; TK_RNUM
logicalOp - TK_AND ; TK_OR
relationalOp - TK_LT ; TK_LE ; TK_EQ ; TK_GT ; TK_GE ; TK_NE
returnStmt - TK_RETURN optionalReturn TK_SEM
optionalReturn - TK_SQL idList TK_SQR ; TK_EPSILON
idList - TK_ID more_ids
more_ids - TK_COMMA idList ; TK_EPSILON
definetypestmt - TK_DEFINETYPE A TK_RUID TK_AS TK_RUID
A - TK_RECORD ; TK_UNION